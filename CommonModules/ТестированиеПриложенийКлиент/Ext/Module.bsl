
Процедура ВыполнитьТест(Параметры) Экспорт 

	Если ТипЗнч(Параметры) = тип("Строка") И 
		СтрНайти(Параметры, "task_") > 0 Тогда
		
		ЗапуститьТестированиеПоУИДУ(СтрЗаменить(Параметры, "task_", ""));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗапуститьТестированиеПоУИДУ(УИД, ЗавершатьРаботуСистемы = Истина) Экспорт 
	
	ДанныеТестирования = ТестированиеПриложенийСервер.ДанныеТестированияПоУИДУ(УИД);
	
	Лог = "";
	Ошибки = Ложь;
	
	ТестированиеПриложенийСервер.ЗафиксироватьНачалоТестирования(УИД);
	
	ЗаписатьВЛог(Лог, "Начало теста");
	
	ТестовоеПриложение = Новый ТестируемоеПриложение("127.0.0.1", ДанныеТестирования.Порт);
	МаксИтераций = 20;
	Подключились = Ложь;
	Для Счетчик = 1 По МаксИтераций Цикл
		
		Попытка
			ТестовоеПриложение.УстановитьСоединение();
			Подключились = Истина;
			Прервать; 
			
		Исключение
			
			//Если сразу не удалось подключится - попробуем запустить клиент тестирования
			Если Счетчик = 3 Тогда
				ЗапуститьПриложение(ДанныеТестирования.СтрокаПодключения);
				//Подождем
				ОбщегоНазначенияСервер.Пауза(ТестированиеПриложенийСервер.ВремяОжиданияЗапускаКлиента());
			КонецЕсли;
			
			Если Счетчик = МаксИтераций Тогда
				ЗаписатьВЛог(Лог, "Не удалось подключиться к клиенту тестирования: " + ОписаниеОшибки());
				Ошибки = Истина;
			КонецЕсли; 
			
		КонецПопытки; 
		
	КонецЦикла;
	
	Если Подключились = Истина Тогда 		
		Для Каждого ТекущийТест Из ДанныеТестирования.Алгоритмы цикл

			Попытка
				Для СчетчикКоличество = 1 по ТекущийТест.Количество цикл
					ЗаписатьВЛог(Лог,"Задание: " + ТекущийТест.Алгоритм + " итерация № "+ СчетчикКоличество);
					ВыполнитьТестовыйАлгоритм(ТестовоеПриложение, ТекущийТест.КодАлгоритма, Лог, СчетчикКоличество, Ошибки);
				КонецЦикла;
			Исключение
				
				ЗаписатьВЛог(Лог,"Тест " + ТекущийТест.Алгоритм+  " завершился с ошибкой: " + ОписаниеОшибки());		
				Ошибки = Истина;
				
			КонецПопытки;
			
		КонецЦикла;
	КонецЕсли;
	
	ЗаписатьВЛог(Лог, "Конец теста");
	
	ТестированиеПриложенийСервер.ЗафиксироватьОкочаниеТестирования(УИД, Лог, Ошибки);
	
	Если ЗавершатьРаботуСистемы Тогда
		ЗавершитьРаботуСистемы(Ложь);
	КонецЕсли;
КонецПроцедуры

Процедура ВыполнитьТестовыйАлгоритм(ТестовоеПриложение, КодАлгоритма, Лог, Итерация, Ошибки) 
	
	Выполнить(КодАлгоритма);
	
КонецПроцедуры

Процедура ЗаписатьВЛог(Лог, Сообщение) Экспорт 
	
	Лог = Лог + ТекущаяДата()+ ": " + Сообщение + Символы.ПС;
	
КонецПроцедуры

Процедура ЗапуститьВсеТесты() Экспорт 
	
	УИДыЗаданий = ТестированиеПриложенийСервер.ПолучитьУИДЫНовыхТестов();
	
	Для Каждого УИД Из УИДыЗаданий цикл
		ЗапуститьМенеджерТеста(УИД);
	КонецЦикла;	
	
КонецПроцедуры

Процедура ЗапуститьМенеджерТеста(УИД) Экспорт 
	
	СтрокаЗапуска = ТестированиеПриложенийСервер.СтрокаПодключенияМенеджераТеста(УИД);
	ЗапуститьПриложение(СтрокаЗапуска);
	
КонецПроцедуры

